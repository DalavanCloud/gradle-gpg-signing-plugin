buildscript {
  repositories {
    mavenCentral()
  }

  dependencies {
    classpath 'info.solidsoft.gradle.pitest:gradle-pitest-plugin:1.1.11'
  }
}
plugins {
  id 'java-gradle-plugin'
  id 'com.github.kt3k.coveralls' version '2.8.1'
}
apply plugin: 'groovy'
apply plugin: 'info.solidsoft.pitest'

repositories {
  mavenCentral()
}

sourceSets {
  integTest
}

configurations {
  integTestCompile.extendsFrom testCompile
  integTestRuntime.extendsFrom testRuntime
}

dependencies {

  compile(
    'commons-io:commons-io:2.5',
  )

  testCompile('org.spockframework:spock-core:1.1-groovy-2.4') {
    exclude group: 'org.codehaus.groovy'
  }
  testCompile(
    'org.codehaus.groovy:groovy-all:2.4.11'
  )

  integTestCompile(
    sourceSets.main.output,
  )

}

group = 'com.yubico.gradle'
version = '0.1.0'

task wrapper(type: Wrapper) {
  gradleVersion = '4.1'
}

task integTest(type: Test) {
  testClassesDirs = sourceSets.integTest.output.classesDirs
  classpath = sourceSets.integTest.runtimeClasspath

  shouldRunAfter test

  reports.html.destination = file("${reporting.baseDir}/${name}")
}
check.dependsOn integTest

pitest {
  testSourceSets = [ sourceSets.test, sourceSets.integTest ]
  timestampedReports = false
  outputFormats = ['XML', 'HTML']
}

tasks.withType(Test) {
  testLogging.showStandardStreams = true
}

coveralls {
  sourceDirs = subprojects.sourceSets.main.allSource.srcDirs.flatten()
}
tasks.coveralls {
  inputs.files tasks.pitest.outputs.files
}
